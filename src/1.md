## [☜ 上一篇 : 引子](https://github.com/rmw-link/rmw-link/blob/master/README.md)

# 人民网络开发笔记 · 2020-08-22

## 初识 NKN ，不用SDK遍历种子节点网络

网络的不平等，来源于底层协议的不平等。

一些网络地址（IP）被称为公网地址，可以对外发布服务。

一些网络地址是内网地址，能访问别人，但别人没法直连它。

这糟糕地底层设计，从源头上造就了如今互联网不平等的格局。

偶然间，看到了[NKN: New Kind Network](https://nkn.org/)，一种新型的网络。

流量证明，这精妙的设计一下吸引了我，让大家通过转发消息来挖矿，借助矿工，让内网的人也能发布公网的服务。

这不是传统的区块链挖矿，算哈希数零，毫无意义地浪费电。

也不是投票选主节点的伪区块链，这是真正对等网络，大赞。

这正是我长期以来一直苦苦寻觅的。

NKN 可以视为因特网 （TCP/IP、Internet），有了这个基础，就可以开发万维网（WWW、World Wide Web）。

打造[人民网络](https://github.com/rmw-link/rmw-link/blob/master/README.md)，就可以从这里开始。

我打算写一个去中心化的浏览器。

## 开发笔记

### 遍历网络获取种子节点

获取种子的节点，是分布式网络最脆的一环。 Resilio Sync 因此挂掉的。

NKN 默认的 rpcServer 是一个官方的节点。但 NKN 是对等网络，可以通过遍历网络来获得更多的种子节点。

今天，我首先尝试抓取种子节点备用。

NKN官方提供了SDK，但是屏蔽了不少细节。为了更好的掌握基础架构，我决定跳过SDK从底层开始学习，方便之后的开发。

不用NKN SDK，直接遍历网络抓取种子节点的代码如下，我存到了sqlite数据库中，方便之后使用。

[代码快照 branch:1_rpc2db](https://github.com/rmw-link/srv/tree/1_rpc2db)

```coffee
import sodium from 'libsodium-wrappers'
import {randomBytes} from 'crypto'
import {post,get} from './lib/axios'
import path from 'path'
import sqlite from 'better-sqlite3'
import string2compact from 'string2compact'
import compact2string from 'compact2string'

sqlite.prototype.all = (query, ...args)->
  @prepare(query).all(...args)

__filename = `import.meta.url.slice(7)`
__dirname = path.dirname __filename

db = sqlite path.join __dirname, 'rpc.db'

db.prepare("""CREATE TABLE IF NOT EXISTS rpc (
id INTEGER,
host BLOB NOT NULL UNIQUE,
alive INTEGER NOT NULL DEFAULT 1,
delay INTEGER NOT NULL,
PRIMARY KEY(id AUTOINCREMENT)
)""").run()

insert_host = db.prepare "INSERT INTO rpc (host,alive,delay) VALUES(?,1,?) ON CONFLICT(host) DO UPDATE SET alive=excluded.alive+1,delay=(excluded.delay+delay)/2"

addressNew = =>
  seedLen = 32
  seed = randomBytes(seedLen)
  key = sodium.crypto_sign_seed_keypair seed
  Buffer.from(key.publicKey).toString('hex')

rpcAddr = (host_port)=>
  try
    {data} = await post(
      "http://"+host_port
      data:
        id: 'nkn-sdk-js',
        jsonrpc: '2.0',
        method: 'getwsaddr',
        params:{
          address:addressNew()
        }
    )
  catch err
    console.log err
    return
  {result} = data
  result?.rpcAddr

class Pool
  constructor:->
    @n = 0
    @$ = new Set()
    @end = new Promise(
      (resolve)=>
        @_resolve = resolve
    )

  push:(host_port)->
    ++@n
    begin = new Date() - 0
    rpc = await rpcAddr host_port
    --@n
    # console.log ">",host_port,rpc
    if not rpc or @$.has rpc
      if @n == 0
        setTimeout(
          =>
            @_resolve()
        )
      return
    delay = new Date() - begin
    if /^\d+\.\d+\.\d+\.\d+\:\d+$/.test(host_port)
      console.log insert_host.run(
        string2compact(host_port), delay
      )
    @$.add rpc
    @push rpc
    rpc



do =>
  await sodium.ready
  pool = new Pool()
  for {host} from db.all "SELECT host FROM rpc"
    host = compact2string host
    await pool.push host
  process.exit()
```

#### 小坑备忘

* axios 的 timeout 是 response timeout 不是 connection timeout

参考 ： [超时不起作用·问题＃647·axios](https://github.com/axios/axios/issues/647)

在浏览器中问题不大，浏览器会负责超时。

但是在nodejs环境下，偶尔会卡死进程，所以我重新封装了一下，参考 [lib/axios.coffee](https://github.com/rmw-link/srv/blob/master/src/lib/axios.coffee)。

